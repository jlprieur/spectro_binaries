C     AMELIORATION DES ELEMENTS ORBITAUX                         *******
C     POUR LES BINAIRES A DEUX SPECTRES                          * BS2 *
C                                                                *******
C     CONSIGNES POUR UTILISATION DU PROGRAMME : NE PAS OUBLIER DE METTRE
C     UN POIDS POUR CHAQUE COMPOSANTE MESUREE. L'ABSENCE DE POIDS SIGNI-
C     FIE QUE LA COMPOSANTE N'A PAS ETE MESUREE. X=1 : PERIODE FIXEE,
C     Y=1 : EXCENTRICITE FIXEE, Y=2 : ORBITE CIRCULAIRE.
C
 1000 FORMAT(F10.7,F10.3,2F10.4,2F10.2,F6.2,2X,2I1)
 1100 FORMAT(I2,6X,I2)
 1200 FORMAT((F9.3,2(F7.1,F6.2,7X)))
 1300 FORMAT(5X,11HCORRECTIONS,15X,17HNOUVEAUX ELEMENTS,15X,12HERREURS S
     1TD.//6X,E9.3,17X,3HP =,F15.7,13X,E9.3//6X,E9.3,17X,3HT =,F11.3,17X
     2,E9.3//6X,E9.3,13X,7HOMEGA =,F13.5,15X,E9.3//6X,E9.3,17X,3HE =,F13
     3.5,15X,E9.3//6X,E9.3,16X,4HK1 =,F10.2,18X,E9.3//6X,E9.3,16X,4HK2 =
     4,F10.2,18X,E9.3//6X,E9.3,16X,4HV0 =,F10.2,18X,E9.3//26X,9HA1.SINI
     5=,6X,E10.4/26X,9HA2.SINI =,6X,E10.4/24X,11HM1.SIN3 I =,6X,E10.4/24
     6X,11HM2.SIN3 I =,6X,E10.4////)
 1400 FORMAT(5X,I3,5X,F9.3,5X,F4.3,2(3(5X,F7.2),5X,F5.2))
 1500 FORMAT(1H1,13X,20HELEMENTS PROVISOIRES//16X,3HP =,F15.7//16X,3HT =
     1,F11.3//12X,7HOMEGA =,F13.5//16X,3HE =,F13.5//15X,4HK1 =,F10.2//15
     2X,4HK2 =,F10.2//15X,4HV0 =,F10.2////)
 1600 FORMAT(5X,4HOBS.,4X,9HDATE (JJ),5X,5HPHASE,4X,8HVR OBS.1,4X,9HVR C
     1ALC.1,5X,6H(O-C)1,4X,7HPOIDS 1,3X,8HVR OBS.2,4X,9HVR CALC.2,5X,6H(
     2O-C)2,4X,7HPOIDS 2/)
 1700 FORMAT(5X,13HPERIODE FIXEE)
 1800 FORMAT(5X,18HEXCENTRICITE FIXEE)
 1801 FORMAT(5X,17HORBITE CIRCULAIRE )
 1900 FORMAT(1H1)
 2000 FORMAT(5X,I3,5X,F9.3,5X,F4.3,17X,F7.2,22X,3(5X,F7.2),5X,F5.2)
 2100 FORMAT(5X,I3,5X,F9.3,5X,F4.3,3(5X,F7.2),5X,F5.2,17X,F7.2)
 2200 FORMAT(5X,I2,11HE ITERATION)
 2300 FORMAT(5X,43HABS(E).GE.1, POURSUITE DU CALCUL IMPOSSIBLE)
      DIMENSION TOBS(99),VIT1(99),VIT2(99),POI1(99),POI2(99)
      DIMENSION RES1(99),RES2(99)
      DIMENSION AA(7,200),B(7),DELT(7),ERR(7),G(7),H(7),EQM(7),CORR(7)
      DIMENSION A(7,7),C(7,7),D(7,7)
      INTEGER R,X,Y,F,DELTA(7)
      REAL KA,K1,K2,M
      OPEN(5,FILE='VR.DAT',STATUS='OLD')
      OPEN(6,FILE='RESULT.DAT',STATUS='NEW')
   88 READ(5,1000)P,T,OMEGA,E,K1,K2,VZERO,X,Y
  881 READ(5,1100)N,NITER
      READ(5,1200)(TOBS(I),VIT1(I),POI1(I),VIT2(I),POI2(I),I=1,N)
      R=7-X-Y
      WRITE(6,1500)P,T,OMEGA,E,K1,K2,VZERO
      ITER=0
C
C     RESOLUTION DE L'EQUATION DE KEPLER
      PI=3.1415926535
      DIST=1.
      N2=N*2
      DO 882 I=1,R
      DO 882 J=1,N2
  882 AA(I,J)=0
   89 DO 9 J=1,R
      B(J)=0
      DO 9 K=1,R
    9 A(J,K)=0
      NU=0
      WRITE(6,1600)
      ENNE=2.*PI/P
      DO53 I=1,N
      PHI=AMOD((TOBS(I)-T)/P,1.)
      IF(PHI.LT.0)PHI=1.+PHI
   92 M=ENNE*(TOBS(I)-T)
      EMME=AMOD(M,2.*PI)
      U=EMME
    1 ANOM=EMME+E*SIN(U)
      IF(ABS(ANOM-U).LT.0.000001)GO TO 2
      U=ANOM
      GO TO 1
C
C     CALCUL DES COEFFICIENTS DES EQUATIONS NORMALES
    2 V=2.*ATAN(TAN(ANOM/2.)*SQRT((1+E)/(1-E)))
      VIC1=VZERO+K1*(E*COS(OMEGA)+COS(V+OMEGA))
      VIC2=VZERO-K2*(E*COS(OMEGA)+COS(V+OMEGA))
      RES1(I)=VIT1(I)-VIC1
      RES2(I)=VIT2(I)-VIC2
      IF(POI1(I).NE.0)GO TO 20
      WRITE(6,2000)I,TOBS(I),PHI,VIC1,VIT2(I),VIC2,RES2(I),POI2(I)
      GO TO 22
   20 IF(POI2(I).NE.0)GO TO 21
      WRITE(6,2100)I,TOBS(I),PHI,VIT1(I),VIC1,RES1(I),POI1(I),VIC2
      GO TO 22
   21 WRITE(6,1400)I,TOBS(I),PHI,VIT1(I),VIC1,RES1(I),POI1(I),VIT2(I),VI
     1C2,RES2(I),POI2(I)
   22 IF(ITER.EQ.NITER.OR.DIST.LT.10.**(-7))GO TO53
      DO 28 K=1,2
      L=I+(K-1)*N
      IF(K.EQ.1)GO TO 23
      IF(K.EQ.2)GO TO 24
   23 IF(POI1(I).EQ.0)GO TO 28
      NU=NU+1
      KA=K1
      OME=OMEGA
      J1=5
      J2=6
      GO TO 25
   24 IF(POI2(I).EQ.0)GO TO 28
      NU=NU+1
      KA=K2
      OME=OMEGA+PI
      J1=6
      J2=5
   25 S=SIN(V+OME)
      Q=S*(1+E*COS(V))**2/(1-E**2)**1.5
      IF(X.EQ.1)GO TO 26
      AA(1,L)=KA*M*Q/P
   26 AA(2-X,L)=KA*ENNE*Q
      IF(Y.EQ.2)GO TO 27
      AA(3-X,L)=-KA*(E*SIN(OME)+S)
      IF(Y.EQ.1)GO TO 27
      AA(4-X,L)=KA*(COS(OME)-S*SIN(V)*(2+E*COS(V))/(1-E**2))
   27 AA(J1-X-Y,L)=E*COS(OME)+COS(V+OME)
      AA(J2-X-Y,L)=0
      AA(R,L)=1.
   28 CONTINUE
      DO43 J=1,R
      B(J)=B(J)+POI1(I)*AA(J,I)*RES1(I)+POI2(I)*AA(J,I+N)*RES2(I)
      DO 3 K=J,R
      A(J,K)=A(J,K)+POI1(I)*AA(J,I)*AA(K,I)+POI2(I)*AA(J,I+N)*AA(K,I+N)
    3 CONTINUE
   43 CONTINUE
   53 CONTINUE
      IF(ITER.EQ.NITER.OR.DIST.LT.10.**(-7))GO TO 81
      DO 4 J=2,R
      JJ=J -1
      DO 4 K=1,JJ
    4 A(J,K)=A(K,J)
C
C     INVERSION DE LA MATRICE A DE RANG R
      DO 10 I=1,R
      DO 10 J=1,R
      D(I,J)=0
   10 D(I,I)=1.
      DO116 K=1,R
      DO 11 I1=1,R
      DO 11 J1=1,R
   11 C(I1,J1)=A(I1,J1)
      IF(A(K,K).NE.0)GO TO 15
      KK =K  +1
      DO 12 L=KK ,R
      IF(A(L,K).EQ.0)GO TO 12
      L1=L
      GO TO 13
   12 CONTINUE
   13 DO 14 J1=1,R
      C(K,J1)=A(L1,J1)
      C(L1,J1)=A(K,J1)
      A(K,J1)=C(K,J1)
      A(L1,J1)=C(L1,J1)
      G(J1)=D(L1,J1)
      H(J1)=D(K,J1)
      D(K,J1)=G(J1)
   14 D(L1,J1)=H(J1)
   15 DO 61 I=1,R
      IF(A(I,K).EQ.0.OR.I.EQ.K)GO TO 61
      DO 16 J=1,R
      A(I,J)=A(I,J)/C(I,K)-A(K,J)/C(K,K)
      D(I,J)=D(I,J)/C(I,K)-D(K,J)/C(K,K)
   16 CONTINUE
   61 CONTINUE
  116 CONTINUE
      DO 17 I=1,R
      DO 17 J=1,R
   17 D(I,J)=D(I,J)/A(I,I)
C
C     NOUVEAUX ELEMENTS - ERREURS
      DO 5 I=1,R
    5 DELT(I)=0
      DO 6 I=1,R
      DO 6 J=1,R
    6 DELT(I)=DELT(I)+D(I,J)*B(J)
      SOMB=0
      DO 7 I=1,N
    7 SOMB=SOMB+POI1(I)*RES1(I)**2+POI2(I)*RES2(I)**2
      SOMD=0
      DO 8 I=1,R
    8 SOMD=SOMD+B(I)*DELT(I)
      SOME=SOMB-SOMD
      SOM=SQRT(SOME/(NU-R))
      DO 80 I=1,R
   80 EQM(I)=SOM*SQRT(D(I,I))
      DIST=0
      F=0
      DO 801 I=1,7
      DELTA(I)=0
      DELTA(1)=X
      IF(Y.EQ.2)DELTA(3)=1
      DELTA(4)=Y
      IF(Y.EQ.2)DELTA(4)=1
      F=F+DELTA(I)
      J=I-F+DELTA(I)
      ERR(I)=(1-DELTA(I))*EQM(J)
      CORR(I)=(1-DELTA(I))*DELT(J)
  801 DIST=DIST+ABS(CORR(I))
      P=P+CORR(1)
      T=T+CORR(2)
      OMEGA=AMOD(OMEGA+CORR(3),2.*PI)
      IF(OMEGA.LT.0)OMEGA=OMEGA+2.*PI
      E=E+CORR(4)
      K1=K1+CORR(5)
      K2=K2+CORR(6)
      VZERO=VZERO+CORR(7)
      A1SINI=0
      A2SINI=0
      FDEM1=0
      FDEM2=0
      IF(ABS(E).GE.1.)GO TO 802
      A1SINI=43200.*P*K1*SQRT(1-E**2)/PI
      A2SINI=43200.*P*K2*SQRT(1-E**2)/PI
      FDEM1=0.00000010385*(K1+K2)**2*K2*P*(1-E**2)**1.5
      FDEM2=0.00000010385*(K1+K2)**2*K1*P*(1-E**2)**1.5
  802 WRITE(6,1900)
      ITER=ITER+1
      WRITE(6,2200)ITER
      IF(X.EQ.1)WRITE(6,1700)
      IF(Y.EQ.1)WRITE(6,1800)
      IF(Y.EQ.2)WRITE(6,1801)
      WRITE(6,1300)CORR(1),P,ERR(1),CORR(2),T,ERR(2),CORR(3),OMEGA,ERR(3
     1),CORR(4),E,ERR(4),CORR(5),K1,ERR(5),CORR(6),K2,ERR(6),CORR(7),VZE
     2RO,ERR(7),A1SINI,A2SINI,FDEM1,FDEM2
      IF(ABS(E).LT.1.)GO TO 89
      WRITE(6,2300)
   81 WRITE(6,1900)
      STOP
      END
